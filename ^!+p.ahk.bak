#Requires AutoHotkey v2.0

; ==============================
; Ctrl+Alt+P / O 핫키
; ==============================

^!+p:: {
    start := A_TickCount            ; 누르기 시작 시간
    KeyWait "p"                     ; 키를 뗄 때까지 대기
    elapsed := A_TickCount - start  ; 누른 시간(ms)

    ; 유튜브 환경
    if InStr(WinGetTitle("A"), "YouTube") {
        SendInput("+.")   ; 재생속도 올리기
        return
    }
    ; Udemy 환경
    else if InStr(WinGetTitle("A"), "Udemy") {
        SendInput("+{Right}")  ; 재생속도 올리기
        return
    }

    ; ========================================
    ; 비주얼 스튜디오 환경에서만 짧게/중간 길이
    ; ========================================
    if WinActive("ahk_exe devenv.exe") {
        if (elapsed < 250) {
            Send "{Right}"              ; 짧게 누르면 오른쪽 방향키
        }
        else if (elapsed >= 250 && elapsed < 550) {
            Send "^!{F12}"             ; 중간 길이 → Ctrl + Alt + F12
        }
        ; 1000ms 이상 → 아무 것도 안 함
    }
}





^!+o:: {
    start := A_TickCount
    KeyWait "o"
    elapsed := A_TickCount - start

    ; =========================
    ; Chrome 환경 (YouTube / Udemy 포함)
    ; =========================
    if WinActive("ahk_exe chrome.exe") {

        title := WinGetTitle("A")
        static magnifierOn := false

        ; --- 짧게 (< 200ms) ---
        if (elapsed < 200) {
            if InStr(title, "YouTube") {
                SendInput "+,"
            }
            else if InStr(title, "Udemy") {
                SendInput "+{Left}"
            }
            return
        }

        ; --- 중간 길이 (200 ~ 450ms) ---
        else if (elapsed >= 200 && elapsed < 450) {
            magnifierOn := !magnifierOn

            if (magnifierOn) {
                ; 돋보기 켜기
                SendInput "{LWin down}={LWin up}"
            } else {
                ; 돋보기 끄기 → 전체화면 복구
                SendInput "{LWin down}{Esc}{LWin up}"  ; Win + Esc
                Sleep 120
                SendInput "f"                         ; 전체화면 다시 켜기
            }
            return
        }
    }

    ; ========================================
    ; 언리얼 엔진 환경
    ; ========================================
    else if InStr(WinGetTitle("A"), "Unreal Editor") {
        if (elapsed < 200) {
            SendInput "{F7}"
            Sleep 120
            SendInput "^s"
        }
        else if (elapsed >= 200 && elapsed < 450) {
            SendInput "+{F1}"
        }
        return
    }

    ; ========================================
    ; 비주얼 스튜디오 환경
    ; ========================================
    if WinActive("ahk_exe devenv.exe") {
        if (elapsed < 250) {
            Send "{Left}"
        }
        else if (elapsed >= 250 && elapsed < 550) {
            Send "{F12}"
        }
    }
}